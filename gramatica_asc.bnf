
/*INICIO DE LA GRAMATICA */
<stmtList> ::= <stmtList> <stmt>
                | <stmt>

<stmt> ::= <selectStmt> ';'
        | <createStmt> ';'
        | <showStmt> ';'
        | <alterStmt> ';'
        | <dropStmt> ';'
        | <insertStmt> ';'
        | <updateStmt> ';'
        | <deleteStmt> ';'
        | <truncateStmt> ';'
        | <useStmt> ';'

/*Expresiones*/
<expresion> ::= <datatype>
            | <expComp>
            | <expBool>
            | '(' <selectStmt> ')'

<datatype> ::=  <colName>
            | <literal>
            | <funcCall>
            | <extract>
            | <datePart>
            | <current>
            | <datatype> '+' <datatype>
            | <datatype> '-' <datatype>
            | <datatype> '/' <datatype>
            | <datatype> '*' <datatype> 
            | <datatype> '%' <datatype>
            | <datatype> '^' <datatype>
            | <datatype> '||' <datatype>
            | <datatype> '&' <datatype>
            | <datatype> '|' <datatype>
            | <datatype> '#' <datatype>
            | '~' <datatype>
            | <datatype> '>>' <datatype>
            | <datatype> '<<' <datatype>
            | '(' <datatype> ')'

<funcCall> ::= funcMath '(' <paramList> ')'
            | funcBool '(' <paramList> ')'
            | funcTrig '(' <paramList> ')'

<paramList> ::= <paramList> ',' <datatype>
            | <datatype>

<extract> ::= EXTRACT '(' <optsExtract> FROM <timeStamp> ')'

<timeStamp> ::= TIMESTAMP stringLit
        | INTERVAL stringLit

<optsExtract> ::= YEAR
                | MONTH
                | DAY
                | HOUR 
                | MINUTE
                | SECOND

<datePart> ::= DATE_PART '(' stringLit ',' <dateSource> ')'

<dateSource> ::= TIMESTAMP stringLit
        | DATE stringLit
        | TIME stringLit
        | INTERVAL <intervalFields> stringLit
        | NOW '(' ')'

<current> ::= CURRENT_DATE
        | CURRENT_TIME
        | <timeStamp>

<expComp> ::= <datatype> '<' <datatype>
            | <datatype> '>' <datatype>
            | <datatype> '>=' <datatype>
            | <datatype> '<=' <datatype>
            | <datatype> '=' <datatype>
            | <datatype> '!=' <datatype>
            | <datatype> '<>' <datatype>
            | <datatype> BETWEEN <datatype> AND <datatype>
            | <datatype> NOT BETWEEN <datatype> AND <datatype>
            | <datatype> BETWEEN SYMMETRIC <datatype> AND <datatype>
            | <datatype> IS DISTINCT FROM <datatype>
            | <datatype> IS NOT DISTINCT FROM <datatype>
            | <datatype> IS NULL
            | <datatype> IS NOT NULL
            | <datatype> ISNULL
            | <datatype> NOTNULL
            | <datatype> IS TRUE
            | <datatype> IS NOT TRUE
            | <datatype> IS FALSE
            | <datatype> IS NOT FALSE
            | <datatype> IS UNKNOWN
            | <datatype> IS NOT UNKNOWN

<expSubq> ::= <datatype> '<'  <subqValues> '(' <selectStmt> ')'
            | <datatype> '>'  <subqValues> '(' <selectStmt> ')'
            | <datatype> '>=' <subqValues> '(' <selectStmt> ')'
            | <datatype> '<=' <subqValues> '(' <selectStmt> ')'
            | <datatype> '='  <subqValues> '(' <selectStmt> ')'
            | <datatype> '!=' <subqValues> '(' <selectStmt> ')'
            | <datatype> '<>' <subqValues> '(' <selectStmt> ')'
            | <datatype> BETWEEN <datatype> AND <datatype> <subqValues> '(' <selectStmt> ')'
            | <datatype> NOT BETWEEN <datatype> AND <datatype> <subqValues> '(' <selectStmt> ')'
            | <datatype> BETWEEN SYMMETRIC <datatype> AND <datatype> <subqValues> '(' <selectStmt> ')'
            | <datatype> IS DISTINCT FFROM <datatype> <subqValues> '(' <selectStmt> ')'
            | <datatype> IS NOT DISTINCT FROM <datatype> <subqValues> '(' <selectStmt> ')'
            | <datatype> IS NULL <subqValues> '(' <selectStmt> ')'
            | <datatype> IS NOT NULL <subqValues> '(' <selectStmt> ')'
            | <datatype> ISNULL <subqValues> '(' <selectStmt> ')'
            | <datatype> NOTNULL <subqValues> '(' <selectStmt> ')'
            | <datatype> IS TRUE <subqValues> '(' <selectStmt> ')'
            | <datatype> IS NOT TRUE <subqValues> '(' <selectStmt> ')'
            | <datatype> IS FALSE <subqValues> '(' <selectStmt> ')'
            | <datatype> IS NOT FALSE <subqValues> '(' <selectStmt> ')'
            | <datatype> IS UNKNOWN <subqValues> '(' <selectStmt> ')'
            | <datatype> IS NOT UNKNOWN <subqValues> '(' <selectStmt> ')'
            | <stringExp> LIKE pattern 

<stringExp> ::= stringLit
        | <colName>

<subqValues> ::= ALL
                | ANY
                | SOME

<boolean> ::= <expComp>
            | litBool
            | EXISTS '(' <selectStmt> ')'
            | <datatype> IN '(' <selectStmt> ')'
            | <datatype> NOT IN '(' <selectStmt> ')'
            | <expSubq>

<expBool> ::= <expBool> AND <expBool>
            | <expBool> OR <expBool>
            | NOT <expBool>
            | <boolean>

<booleanCheck> ::= <expComp>
            | litBool

<expBoolCheck> ::= <expBoolCheck> AND <booleanCheck>
            | <expBoolCheck> OR <booleanCheck>
            | NOT <expBoolCheck>
            | <booleanCheck>

<literal> ::= litBool
            | litString
            | litNum
            | litChar

/*DDL*/
<createStmt> ::= CREATE <createBody>

<createBody> ::= OR REPLACE <createOpts>
             | <createOpts>

<createOpts> ::= TABLE <ifNotExists> id '(' <createTableList> ')' <inheritsOpt>
            | DATABASE <ifNotExists> id <createOwner> <createMode>
            | TYPE <ifNotExists> id AS ENUM '(' <paramList> ')'

<inheritsOpt> ::= INHERITS '(' id ')'
                |

<ifNotExists> ::= IF NOT EXISTS
                | 

<createTableList> ::= <createTableList> ',' <createTable>
                    | <createTable>

<createTable> ::= id <type> <createOpts>
                | <createConstraint>
                | <createUnique>
                | <createPrimary>
                | <createForeign>

<type> ::= id
        | SMALLINIT
        | INTEGER
        | BIGINIT 
        | DECIMAL
        | NUMERIC
        | REAL
        | DOUBLE PRECISION
        | MONEY
        | CHARACTER VARYING <optParams>
        | VARCHAR <optParams>
        | CHARACTER <optParams>
        | CHAR <optParams>
        | TEXT
        | <timeType>

<timeType> ::= TIMESTAMP <optParams>
        | DATE
        | TIME <optParams>
        | INTERVAL <intervalFields> <optParams>

<intervalFields> ::= YEAR
                | MONTH
                | DAY
                | HOUR
                | MINUTE
                | SECOND
                |

<optParams> ::= '(' <literalList> ')'

<literalList> ::= <literalList> ',' <literal>
                | <literal>

<createOpts> ::= <colOptionsList>
            | 

<createConstraint> ::= <constrName> CHECK '(' <expBoolCheck> ')'

<createUnique> ::= UNIQUE '(' <idList> ')'

<idList> ::= <idList> ',' id
        | id

<createPrimary> ::= PRIMARY KEY '(' <idList> ')'

<createForeign> ::= FOREIGN KEY '(' <idList> ')' REFERENCES id '(' <idList> ')'

<constrName> ::= CONSTRAINT id 
            |

<colOptionsList> ::= <colOptionsList> <colOptions>
                | <colOptions>

<colOptions> ::= <defaultVal>
            | <nullOpt>
            | <constraintOpt>
            | <primaryOpt>
            | <referencesOpt>

<defaultVal> ::= DEFAULT <datatype>

<nullOpt> ::= NOT NULL
            | NULL

<constraintOpt> ::= <constrName> UNIQUE
                | <constrName> CHECK '(' <expBoolCheck> ')'

<primaryOpt> ::= PRIMARY KEY

<referencesOpt> ::= REFERENCES id

<createOwner> ::= OWNER id
                | OWNER '=' id
                |

<createMode> ::= MODE number
             | MODE '=' number
             |

<alterStmt> ::= ALTER DATABASE id <alterDb>
            | ALTER TABLE id <alterTableList>

<alterDb> ::= RENAME TO id
            | OWNER TO <ownerOPts>

<ownerOPts> ::= id
            | CURRENT_USER
            | SESSION_USER

<alterTableList> ::= <alterTableList> ',' <alterTable>
                | <alterTable>

<alterTable> ::= ADD <alterConstraint>
            | <alterCol>
            | DROP CONSTRAINT id
            | DROP COLUMN id
            | RENAME COLUMN id TO id

<alterConstraint> ::= CHECK '(' <expBoolCheck> ')'
                    | CONSTRAINT id UNIQUE '(' id ')'
                    | <createForeign>
                    | COLUMN id <type>

<alterCol> ::= ALTER COLUMN id SET NOT NULL
            | ALTER COLUMN id SET NULL
            | ALTER COLUMN id TYPE <type>

<dropStmt> ::= DROP TABLE id
            | DROP DATABASE <ifExists> id

<ifExists> ::= IF EXISTS 
            |

/*DML*/
<selectStmt> ::= SELECT <selectParams> FROM <tableExp> <joinList> <whereCl> <groupByCl> <orderByCl> <limitCl>
            | SELECT DISTINCT <selectParams> FROM <tableExp> <whereCl> <groupByCl>
            | <selectStmt> UNION <allOpt> <selectStmt>
            | <selectStmt> INTERSECT <allOpt> <selectStmt>
            | <selectStmt> EXCEPT <allOpt> <selectStmt>
            | '(' <selectStmt> ')'

<allOpt> ::= ALL
            |

<selectParams> ::= '*'
                | <selectList>

<selectList> ::= <selectList> ',' <expresion> <optAlias>
            | <expresion> <optAlias>

<optAlias> ::= AS id
            | id
            |

<tableExp> ::= <tableExp> ',' <fromBody> <optAlias>
            | <fromBody> <optAlias>

<colName> ::= id
            | id.id

<fromBody> ::= <colName>
            | '(' <selectStmt> ')'

<joinList> ::= <joinList> <joinCl>
            | <joinCl>

<joinCl> ::= <joinOpt> JOIN <colName> ON <expBool>
        | <joinOpt> JOIN <colName> USING '(' <nameList> ')'
        | NATURAL <joinOpt> JOIN <colName>
        |

<nameList> ::= <nameList> ',' <colName>
            | <colName>

<joinOpt> ::= INNER
            | LEFT 
            | LEFT OUTER
            | RIGHT
            | RIGHT OUTER
            | FULL
            | FULL OUTER

<whereCl> ::= WHERE <expBool>
            | /*epsilon*/

<groupByCl> ::= GROUP BY <groupList> <havingCl>
            | 

<groupList> ::=  <groupList> ',' <colName>
            | <groupElem>

<havingCl> ::= HAVING <expBool>
            |

<orderByCl> ::= ORDER BY <orderList>

<orderList> ::= <orderList> ',' <orderByElem>
            | <orderByElem>

<orderByElem> ::= <colName> <orderOpts> <orderNull>

<orderOpts> ::= ASC
            | DESC
            |

<orderNull> ::= NULL FIRST
            | NULL LAST
            |

<limitCl> ::= LIMIT number <offsetLimit>
            | LIMIT ALL <offsetLimit>
            |

<offsetLimit> ::= OFFSET number
                |

<insertStmt> ::= INSERT INTO id VALUES '(' <paramList> ')'

<updateStmt> ::= UPDATE id <optAlias> SET <updateCols> '=' <updateVals> <whereCl>

<updateCols> ::= id
                | '(' <idList> ')'

<updateVals> ::= <updateExp>
                | '(' <updateList> ')'

<updateList> ::= <updateList> ',' <updateExp>
                | <updateExp>

<updateExp> ::= <datatype>
                | DEFAULT

<deleteStmt> ::= DELETE FROM id <optAlias> <whereCl>

<truncateStmt> ::= TRUNCATE <tableOpt> id

<tableOpt> ::= TABLE
        | 

/*Otros*/
<showStmt> ::= SHOW DATABASES <likeOpt>

<likeOpt> ::= LIKE regex
            |

<useStmt> ::= USE DATABSE id